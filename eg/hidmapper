#!/usr/bin/env perl
use v5.30.0;
use warnings;

use Getopt::Long::Descriptive;
use Wink::Util qw(get_serial_dev_map);

my ($opt, $usage) = describe_options(
  '%c %o',
  [ 'sn=s@', 'serial numbers, in order, to mount as device 0..n' ],
);

unless ($opt->sn && $opt->sn->@*) {
  die "You didn't specify any serial numbers with --sn, so nothing to do!\n";
}

my @serials = $opt->sn->@*;

my @devices = @ARGV;

unless (@devices) {
  die "You didn't specify any USB devices to mount!\n";
}

{
  my @bad_devices = grep {; ! -w } @devices;

  if (@bad_devices) {
    my $error = "You don't seem to have write access to some devices!\n";
    $error .= "- $_\n" for @bad_devices;
    die $error;
  }
}

my $map = get_serial_dev_map(\@devices);

my @missing = grep {; ! $map->{$_} } @serials;

if (@missing) {
  my $error = "Requested serial numbers not found:\n";
  $error .= "- $_\n" for @missing;
  die $error;
}

my $bank = join q{,},
           map {; "$_:hidraw:$map->{$serials[$_]}" } keys @serials;

say $bank;
